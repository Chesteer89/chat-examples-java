apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

check.dependsOn('checkstyle')

def snippetsPath = "snippets"
def examplesPath = "examples"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.pubnub.crc.android"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        buildConfigField "String", "PUB_KEY", PUB_KEY
        buildConfigField "String", "SUB_KEY", SUB_KEY
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    lintOptions {
        textReport = true
    }
    sourceSets {
        main {
            manifest.srcFile "$examplesPath/AndroidManifest.xml"
            java.srcDirs = ["$examplesPath"]
            res.srcDirs = ["$examplesPath/res"]
        }
        test {
            java.srcDirs = ["$snippetsPath"]
        }
    }
}

checkstyle {
    toolVersion = '8.14'
    configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
}

task checkstyle(type: Checkstyle) {
    source "$snippetsPath"
    include '**/*.java'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'
    classpath = files()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.jakewharton:butterknife:10.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'
    implementation 'com.pubnub:pubnub-gson:4.22.0-beta'

    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'

    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.awaitility:awaitility:3.1.5'
}
